#Использовать fs

Перем ИмяПараметра;
Перем ЗначениеПараметра;

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	Команда = Парсер.ОписаниеКоманды(ИмяКоманды, "Установить значение настройки");
	Парсер.ДобавитьКоманду(Команда);
	Парсер.ДобавитьПараметрФлагКОманды(Команда, "-g", "Признак установки настройки глобально.");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ИмяПараметра", "Имя параметра, значение которого надо установить.");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ЗначениеПараметра", "Устанавливаемое значение параметра.");
КонецПроцедуры

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие ключей командной строки и их значений
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды) Экспорт
    ИмяПараметра = ПараметрыКоманды["ИмяПараметра"];
	ЗначениеПараметра = ПараметрыКоманды["ЗначениеПараметра"];
	Глобально = ПараметрыКоманды["-g"];

	ИтоговыйТекстФайла = "";

	ИмяФайлаНастроек = "oscript.cfg";
	Если Глобально Тогда
		РасположениеФайлаНастроек = КаталогПрограммы();
	Иначе
		РасположениеФайлаНастроек = ТекущийКаталог();
	КонецЕсли;
	ПутьКФайлуНастроек = ОбъединитьПути(РасположениеФайлаНастроек, ИмяФайлаНастроек);

	// Создание файла настроек, если его нет.
	Если НЕ ФС.ФайлСуществует(ПутьКФайлуНастроек) Тогда
		ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлуНастроек, КодировкаТекста.UTF8NoBOM);
		ЗаписьТекста.Закрыть();
	КонецЕсли;

	НастройкаНайденаВФайле = Ложь;

	// Чтение файла настроек и формирование итогового текста файла
	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлуНастроек, КодировкаТекста.UTF8NoBOM);
	ДанныеФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	ЧислоСтрок = СтрЧислоСтрок(ДанныеФайла);
	Для сч = 1 По ЧислоСтрок Цикл
		СтрокаФайла = СтрПолучитьСтроку(ДанныеФайла, сч);
		СтрокаДляЗаписи = "";
		ЭтоНезначащаяСтрока = СокрЛП(СтрокаФайла) = "" ИЛИ Лев(СокрЛП(СтрокаФайла), 1) = "#";
		Если ЭтоНезначащаяСтрока Тогда
			СтрокаДляЗаписи = СтрокаФайла;
		Иначе
			ДанныеСтроки = СтрРазделить(СтрокаФайла, "=");
			Если СокрЛП(ДанныеСтроки[0]) = ИмяПараметра Тогда
				СтрокаДляЗаписи = ПолучитьСтрокуНастройки();
				НастройкаНайденаВФайле = Истина;
			Иначе
				СтрокаДляЗаписи = СтрокаФайла;
			КонецЕсли;
		КонецЕсли;
		
		ИтоговыйТекстФайла = ИтоговыйТекстФайла + СтрокаДляЗаписи + Символы.ПС;
		
	КонецЦикла;

	Если НЕ НастройкаНайденаВФайле Тогда
		СтрокаДляЗаписи = ПолучитьСтрокуНастройки();
		ИтоговыйТекстФайла = ИтоговыйТекстФайла + СтрокаДляЗаписи + Символы.ПС;	
	КонецЕсли;

	// Удаление последнего добавленного перевода строки для предотвращения расползания файла
	ИтоговыйТекстФайла = Лев(ИтоговыйТекстФайла, СтрДлина(ИтоговыйТекстФайла) - 1);

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлуНастроек, КодировкаТекста.UTF8NoBOM);
	ЗаписьТекста.Записать(ИтоговыйТекстФайла);
	ЗаписьТекста.Закрыть();

	Возврат 0;
КонецФункции

Функция ПолучитьСтрокуНастройки()
	Возврат СтрШаблон("%1 = %2", ИмяПараметра, ЗначениеПараметра);
КонецФункции
